; ############################################################################
; T.B. Moraes, et al.; Steady-State Free Precession sequences for high and low field 
; NMR spectroscopy in solution: Challenges and opportunities, JMRO, 2022
; Eight block constant phase increment (SSFPd8)
; 1D sequence with power-gated decoupling
; ############################################################################
; Set:  DS = 1 ; NS = 1;  d3 = Aq + d1; 
; Recommended values: d1 = 0.01001 ms (minimum); Aq = 0.189 ms; d3 = 0.2 ms;
; L0 - dummy scan of each phase increment block; 
; L1 - scan in each phase increment block; L0 e L1 need to be multiple of 8;

#include <Avance.incl>
#include <Delay.incl>

"d11=30m"
"DELTA=d1-10m"
"acqt0=-p1*0.66/3.1416"

1 ze
  d11 pl12:f2
2 30m do:f2
  10u pl13:f2
  d11 cpd2:f2
  DELTA
  4u do:f2
  10u pl12:f2
  100m cpd2:f2
3  (p1 pl1 ph1^):f1
   d3 
   lo to 3 times l0
4  (p1 pl1 ph1^):f1
   gosc ph11 
   d1 ipp11
   lo to 4 times l1
5  (p1 pl1 ph2^):f1
   d3 
   lo to 5 times l0
6  (p1 pl1 ph2^):f1
   gosc ph12
   d1 ipp12
   lo to 6 times l1
7  (p1 pl1 ph3^):f1
   d3 
   lo to 7 times l0
8  (p1 pl1 ph3^):f1
   gosc ph13
   d1 ipp13
   lo to 8 times l1     
9  (p1 pl1 ph4^):f1
   d3 
   lo to 9 times l0
10  (p1 pl1 ph4^):f1
   gosc ph14
   d1 ipp14
   lo to 10 times l1
11 (p1 pl1 ph5^):f1
   d3 
   lo to 11 times l0
12 (p1 pl1 ph5^):f1
   gosc ph15
   d1 ipp15
   lo to 12 times l1
13 (p1 pl1 ph6^):f1
   d3 
   lo to 13 times l0
14 (p1 pl1 ph6^):f1
   gosc ph16
   d1 ipp16
   lo to 14 times l1
15 (p1 pl1 ph7^):f1
   d3 
   lo to 15 times l0
16 (p1 pl1 ph7^):f1
   gosc ph17
   d1 ipp17
   lo to 16 times l1
17 (p1 pl1 ph8^):f1
   d3 
   lo to 17 times l0
18 (p1 pl1 ph8^):f1
   gosc ph18
   d1 ipp18
   lo to 18 times l1
  30m do:f2 pl13:f2 mc #0 to 2 F0(zd)
exit

;Phase increment 0
ph1=  0 
ph11= 0  

;Phase increment 45
ph2=  (8) 0 1 2 3 4 5 6 7 
ph12= (8) 0 1 2 3 4 5 6 7

;Phase increment 90
ph3=  0 1 2 3  
ph13= 0 1 2 3  

;Phase increment 135
ph4=  (8) 0 3 6 1 4 7 2 5
ph14= (8) 0 3 6 1 4 7 2 5 

;Phase increment 180
ph5=  0 2  
ph15= 0 2

;Phase increment 225
ph6=  (8) 0 5 2 7 4 1 6 3
ph16= (8) 0 5 2 7 4 1 6 3

;Phase increment 270
ph7=   0 3 2 1
ph17=  0 3 2 1

;Phase increment 315
ph8=  (8) 7 6 5 4 3 2 1 0
ph18= (8) 7 6 5 4 3 2 1 0 
